apply plugin: 'idea'
apply plugin: 'maven'

group = 'com.timepath'
version = '1.0-SNAPSHOT'

apply plugin: 'java'
sourceCompatibility = 1.7
targetCompatibility = 1.7

apply plugin: 'maven-publish'

task jarSources(type: Jar) {
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact jarSources {
                classifier 'sources'
            }
        }
    }
}

def pubCreds = new File(System.properties['user.home'], '.m2/publish.groovy')
if (pubCreds.exists() || (System.env['CI_DEPLOY_USERNAME'] && System.env['CI_DEPLOY_PASSWORD'])) {
    uploadArchives {
        repositories.mavenDeployer {
            // 'oss-release-local'
            repository(url: 'https://oss.jfrog.org/artifactory/oss-snapshot-local') {
                def creds = [
                        userName: System.env['CI_DEPLOY_USERNAME'],
                        password: System.env['CI_DEPLOY_PASSWORD']
                ]
                if (pubCreds.exists()) {
                    def b = new Binding()
                    b.setVariable("creds", creds)
                    new GroovyShell(b).evaluate(pubCreds.toURI())
                }
                authentication(creds)
            }
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://oss.jfrog.org/artifactory/oss-snapshot-local' }
    maven { url 'https://sourceutils.github.io/super' }
}

dependencies {
    compile 'org.jetbrains:annotations:13.0'
    compile 'org.kohsuke.metainf-services:metainf-services:1.5'
    testCompile 'junit:junit:4.11'
}
